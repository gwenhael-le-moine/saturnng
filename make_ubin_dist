#!/bin/csh
#
# $Id: make_ubin_dist,v 4.1 2000/12/11 09:54:19 cibrario Rel $
#
# This script builds a user-level binary distribution for release
# $argv[1].  The name of the binary distribution tar
# includes $argv[1] as its last component.
#
# $argv[2] contains the make Makefile command

if( $#argv < 1 ) then
    echo "make_ubin_dist: missing target release number"
    exit 1
endif

if( $#argv < 2 ) then
    echo "make_ubin_dist: missing make Makefile command"
    exit 1
endif

set rel = $argv[1]
set src_tar = saturn_src_$rel.tar
set ubin_tar = saturn_ubin_$rel.tar

set special_flist = (sutil_48.lib sutil_49.lib)

set bin_flist = (README.txt BUILD_INFO.txt saturn Saturn.ad saturn.cat run_saturn quick_start saturn.ps saturn.info pack)

./make_src_dist $rel
if( $status != 0  && $status != 2 ) then
    echo "make_ubin_dist: make_src_dist failed"
    exit 2
endif

mkdir tmp_ubin; cd tmp_ubin

tar xvf ../$src_tar
if( $status != 0 ) then
    echo "make_ubin_dist: source tar extract failed"
    exit 3
endif

cat - > BUILD_INFO.txt <<EOF
This is a user-level binary distribution of saturn release $rel.

   saturn - A poor-man's emulator of some HP calculators
   Copyright (C) 1998-2000 Ivan Cibrario Bertolotti

   This program is free software; you can redistribute it and/or modify
   it under the terms of the GNU General Public License as published by
   the Free Software Foundation; either version 2 of the License, or
   (at your option) any later version.

   This program is distributed in the hope that it will be useful,
   but WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
   GNU General Public License for more details.

   You should have received a copy of the GNU General Public License
   along with the documentation of this program; if not, write to
   the Free Software Foundation, Inc.,
   59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.

   For more information, please contact the author, preferably by email,
   at the following address:

   Ivan Cibrario Bertolotti
   IRITI - National Research Council
   c/o IEN "Galileo Ferraris"
   Strada delle Cacce, 91
   10135 - Torino (ITALY)

   email: cibrario@iriti.cnr.it

The distribution was created on:
  `date`
The build host was (uname -a):
  `uname -a`
EOF

if( $status != 0 ) then
    echo "make_ubin_dist: can't create BUILD_INFO.txt"
    exit 4
endif

$argv[2]
make depend; make; make doc

# Special: Copy special binaries down
foreach special ($special_flist)
    cp ../$special .
end

tar cvf ../$ubin_tar $bin_flist $special_flist

if( $status != 0 ) then
    echo "make_ubin_dist: can't create binary tar"
    exit 5
endif

cd ..
rm -rf tmp_ubin
